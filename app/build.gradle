plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    namespace "org.androidaudioplugin.ports.juce.byod"
    compileSdk = 34

    defaultConfig {
        applicationId "org.androidaudioplugin.ports.juce.byod"
        minSdk = 29
        ndk {
            abiFilters "x86", "x86_64", "arm64-v8a"
        }
        externalNativeBuild {
            cmake {
                arguments "-DAAP_DIR=${System.env.AAP_DIR != null ? System.env.AAP_DIR : "$projectDir/../external/aap-core"}",
                        "-DAAP_JUCE_DIR=${System.env.AAP_JUCE_DIR != null ? System.env.AAP_JUCE_DIR : "$projectDir/../external/aap-juce"}",
                        "-DANDROID_STL=c++_shared", "-DANDROID_CPP_FEATURES=exceptions rtti", "-DANDROID_ARM_NEON=ON"
            }
        }
        proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }

    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
            version libs.versions.cmake.get()
        }
    }

    sourceSets {
        main.java.srcDirs +=
            ["../external/BYOD/modules/JUCE/modules/juce_gui_basics/native/javaopt/app",
             "../external/aap-juce/java"]
        main.res.srcDirs +=
            []
    }

    dependencies {
        implementation libs.androidx.core.ktx
        implementation libs.startup.runtime
        implementation libs.aap.core
        implementation libs.aap.ui.web
        implementation libs.aap.ui.compose
        // switch to them when you want to debug into aap-core.
        /*
        implementation project(":androidaudioplugin")
        implementation project(":androidaudioplugin-ui-web")
        implementation project(":androidaudioplugin-ui-compose")
         */
    }

    buildFeatures {
        prefab true
    }

    ndkVersion libs.versions.ndk.get()
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

